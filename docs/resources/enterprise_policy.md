---
# generated by https://github.com/hashicorp/terraform-plugin-docs
page_title: "powerplatform_enterprise_policy Resource - Power Platform"
subcategory: ""
description: |-
  Enterprise Policy environment assignment
---

# powerplatform_enterprise_policy (Resource)

Enterprise Policy environment assignment

## Example Usage

```terraform
terraform {
  required_version = "> 1.7.0"
  required_providers {
    powerplatform = {
      source  = "microsoft/power-platform"
      version = "~>3.0"
    }
    azapi = {
      source  = "azure/azapi"
      version = "~>2.0"
    }
    azurerm = {
      source  = "hashicorp/azurerm"
      version = "~>4.8"
    }
  }
}
provider "powerplatform" {
  use_cli = true
}

provider "azurerm" {
  use_cli         = true
  subscription_id = var.subscription_id
  features {
    resource_group {
      prevent_deletion_if_contains_resources = false
    }
  }
}

// getting all locations available for the environment and their azure regions
data "powerplatform_locations" "all_locations" {
}

// getting european loocation details. Policy has to be in the same azure region as the environment
locals {
  europe_location = [for location in data.powerplatform_locations.all_locations.locations : location if location.name == "europe"]
}

// creating environment that will have the policies applied
resource "powerplatform_environment" "example_environment" {
  display_name     = "example_environment"
  location         = local.europe_location[0].name
  environment_type = "Sandbox"
  dataverse = {
    language_code     = "1033"
    currency_code     = "USD"
    security_group_id = "00000000-0000-0000-0000-000000000000"
  }
}

// creating managed environment for the environment as this is required for encryption policy
resource "powerplatform_managed_environment" "managed_development" {
  environment_id             = powerplatform_environment.example_environment.id
  is_usage_insights_disabled = true
  is_group_sharing_disabled  = true
  limit_sharing_mode         = "ExcludeSharingToSecurityGroups"
  max_limit_user_sharing     = 10
  solution_checker_mode      = "None"
  suppress_validation_emails = true
}


// module that creates all azure resources required for the network injection policy and the policy itself
module "network_injection" {
  source = "./network_injection"

  should_register_provider = false

  environment_id = powerplatform_environment.example_environment.id

  resource_group_name        = "rg_example_network_injection_policy"
  resource_group_location    = local.europe_location[0].azure_regions[0]
  vnet_locations             = local.europe_location[0].azure_regions
  enterprise_policy_name     = "ep_example_network_injection_policy"
  enterprise_policy_location = local.europe_location[0].name
}

// module that creates all azure resources required for the encryption policy and the policy itself
module "encryption" {
  source = "./encryption"

  should_register_provider = false

  environment_id = powerplatform_environment.example_environment.id

  resource_group_name        = "rg_example_encryption_policy8"
  resource_group_location    = local.europe_location[0].azure_regions[0]
  enterprise_policy_name     = "ep_example_encryption_policy8"
  enterprise_policy_location = "europe"
  keyvault_name              = "kv-ep-example8"

  // let's wait for first policy to be executed
  depends_on = [powerplatform_enterprise_policy.network_injection]
}
```

<!-- schema generated by tfplugindocs -->
## Schema

### Required

- `environment_id` (String) Environment id
- `policy_type` (String) Policy type [NetworkInjection, Encryption]
- `system_id` (String) Policy SystemId value in following format `/regions/<location>/providers/Microsoft.PowerPlatform/enterprisePolicies/<policyid>`

### Optional

- `timeouts` (Attributes) (see [below for nested schema](#nestedatt--timeouts))

### Read-Only

- `id` (String) Id of the resource

<a id="nestedatt--timeouts"></a>
### Nested Schema for `timeouts`

Optional:

- `create` (String) A string that can be [parsed as a duration](https://pkg.go.dev/time#ParseDuration) consisting of numbers and unit suffixes, such as "30s" or "2h45m". Valid time units are "s" (seconds), "m" (minutes), "h" (hours).
- `delete` (String) A string that can be [parsed as a duration](https://pkg.go.dev/time#ParseDuration) consisting of numbers and unit suffixes, such as "30s" or "2h45m". Valid time units are "s" (seconds), "m" (minutes), "h" (hours). Setting a timeout for a Delete operation is only applicable if changes are saved into state before the destroy operation occurs.
- `read` (String) A string that can be [parsed as a duration](https://pkg.go.dev/time#ParseDuration) consisting of numbers and unit suffixes, such as "30s" or "2h45m". Valid time units are "s" (seconds), "m" (minutes), "h" (hours). Read operations occur during any refresh or planning operation when refresh is enabled.
- `update` (String) A string that can be [parsed as a duration](https://pkg.go.dev/time#ParseDuration) consisting of numbers and unit suffixes, such as "30s" or "2h45m". Valid time units are "s" (seconds), "m" (minutes), "h" (hours).
